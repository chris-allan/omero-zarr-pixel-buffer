plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
}

group = 'com.glencoesoftware.omero'
version = '0.2.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.releases'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/ome.external'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/bioformats2raw2ometiff/'
    }
    maven {
        url 'https://repo.glencoesoftware.com/repository/jzarr-snapshots'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/scijava-thirdparty'
    }
    maven {
        name 'Brockmann-Consult Public Maven Repository'
        url 'http://nexus.senbox.net/nexus/content/groups/public/'
    }
}

dependencies {
    api 'org.openmicroscopy:omero-blitz:5.6.2'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.3'
    implementation 'com.bc.zarr:jzarr:0.3.4'
    implementation 'org.lasersonlab:s3fs:2.2.3'
}

test {
  useJUnit()
}

publishing {
    publications {
        omeroZarrPixelBuffer(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-private-release-local/'
            credentials {
               username = project.properties.ArtifactoryUserName
               password = project.properties.ArtifactoryPassword
            }
        }
    }
}

jar {
    manifest {
        attributes(
            "Created-By": "Gradle ${gradle.gradleVersion}",
            "Build-Jdk": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            "Built-By": System.properties['user.name'],
            "Built-On": new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            "Implementation-Build": "git rev-parse --verify HEAD".execute().getText().trim(),
            "Implementation-Title": "OMERO Zarr Pixel Buffer",
            "Implementation-Version": archiveVersion,
            "Implementation-Vendor": "Glencoe Software Inc.",
        )
    }
}
